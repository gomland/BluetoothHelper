apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = project.ext.bintrayGroupId
version = project.ext.bintrayVersionName

install {
    repositories.mavenInstaller {
        pom.project {
            name project.name
            description project.ext.bintrayDescription
            url project.ext.bintraySiteUrl
            inceptionYear '2018' // release date

            packaging 'aar'
            groupId project.ext.bintrayGroupId
            artifactId project.ext.bintrayArtifactId
            version project.ext.bintrayVersionName

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                connection project.ext.bintrayGitUrl
                developerConnection project.ext.bintrayGitUrl
                url project.ext.bintraySiteUrl
            }

            developers {
                developer {
                    id 'shinsung'
                    name 'shinsung'
                    email 'gomland@naver.com'
                }
            }
        }

        pom.whenConfigured { pom ->
            pom.dependencies.forEach { dep ->
                if (dep.getVersion() == "unspecified") {
                    dep.setGroupId(project.ext.bintrayGroupId)
                    dep.setVersion(project.ext.bintrayVersionName)
                }
            }
        }
    }
}

bintray {
    user = project.ext.user
    key = project.ext.key

    configurations = ['archives']

    pkg {
        repo = project.ext.bintrayRepo
        name = project.name
        userOrg = project.ext.bintrayUserOrg
        desc = project.ext.bintrayDescription

        websiteUrl = project.ext.bintraySiteUrl
        vcsUrl = project.ext.bintrayGitUrl

        licenses = ['Apache-2.0']
        labels = project.ext.bintrayLabels

        publish = true
        publicDownloadNumbers = true

        version {
            name = project.ext.bintrayVersionName
            desc = "${project.ext.bintrayDescription} ${project.ext.bintrayVersionName}"
            released = new Date()
            vcsTag = project.ext.bintrayVersionName
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

task javadoc(type: Javadoc) {
    title = "ver.${project.HELPER_VERSION}"
    failOnError false

    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    include '**/Bluetooth.java'
    include '**/BluetoothListener.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}